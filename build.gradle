plugins {
    id "architectury-plugin" version "3.4.+"
    id "dev.architectury.loom" version "0.12.+" apply false
    id "io.github.juuxel.loom-quiltflower" version "1.+" apply false
    id "org.quiltmc.quilt-mappings-on-loom" version "4.+"
    id "com.github.johnrengelman.shadow" version "7.+" apply false
    id "maven-publish"
}

architectury.minecraft = rootProject.minecraft_version

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "io.github.juuxel.loom-quiltflower"

    archivesBaseName = "$rootProject.archives_base_name-$project.name"

    dependencies {
        minecraft "com.mojang:minecraft:$rootProject.minecraft_version"
        mappings loom.layered {
            addLayer quiltMappings.mappings("org.quiltmc:quilt-mappings:$rootProject.minecraft_version+build.$rootProject.mappings_version:v2"
        )}
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    targetCompatibility = JavaVersion.VERSION_17
    sourceCompatibility = JavaVersion.VERSION_17

    group = "io.github.consistencyplus"
    version = "$rootProject.mod_version+$rootProject.minecraft_version"

    tasks.withType(JavaCompile).forEach {
        it.options.release.set(17)
        it.options.encoding = "UTF-8"
    }

    repositories {
        maven { url "https://maven.bai.lol" }
        maven {
            url = "https://api.modrinth.com/maven"
            content { includeGroup "maven.modrinth" }
        }
        maven {
            url = "https://maven.bai.lol"
            content { includeGroup "mcp.mobius.waila" }
        }
    }

    jar {
        from "LICENSE"
        exclude "**/upcoming/**"
        exclude "**/unused/**"
    }

    java.withSourcesJar()
}
