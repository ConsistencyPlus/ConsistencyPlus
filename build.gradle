plugins {
    id "fabric-loom" version "0.8-SNAPSHOT"
    id "org.ajoberstar.grgit" version "4.1.0"
    id "java-library"
    // id "io.github.juuxel.loom-quiltflower" version "1.1.1"
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = "consistency_plus-${project.minecraft_version}"
version = "${project.mod_version}"

repositories {
    maven { url = "https://api.modrinth.com/maven" }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "maven.modrinth:lazydfu:0.1.2"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) { it.options.encoding = "UTF-8" }

jar { from("LICENSE") }

def getVersionMetadata() {
    def build_id = System.getenv("GITHUB_RUN_NUMBER")
    if (build_id != null) { return "build.${build_id}" }
    if (grgit != null) {
        def head = grgit.head()
        def id = head.abbreviatedId
        if (!grgit.status().clean) { id += "-dirty" }
        return "rev.${id}"
    }
    return "unknown"
}
