plugins {
    id "architectury-plugin" version "3.+"
    id "dev.architectury.loom" version "1.0.+" apply false
    id "io.github.juuxel.loom-quiltflower" version "1.+" apply false
    id "com.github.johnrengelman.shadow" version "7.+" apply false
    id "maven-publish"
}

architectury.minecraft = rootProject.minecraft_version

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "io.github.juuxel.loom-quiltflower"

    archivesBaseName = "$archives_base_name-$project.name"

    //Mapping idea: QM on Yarn on parchment on Moj? We need Moj with tweaks tbh
    dependencies {
        minecraft "com.mojang:minecraft:$minecraft_version"
        mappings "net.fabricmc:yarn:1.19.2+build.$yarn_build:v2"

        String platform = project.name
        if (platform == "common") return

        modImplementation("dev.architectury:architectury-$platform:$architectury_version")

        modImplementation("maven.modrinth:stonecutter_recipe_tags:$scrt_version-$platform")
        modImplementation("com.github.LlamaLad7:MixinExtras:0.1.1") // for SCRT

        modCompileOnly("mcp.mobius.waila:wthit-api:$platform-$wthit_version")// compile against the WTHIT API

        if (waila_fork == "wthit") {
            modLocalRuntime("mcp.mobius.waila:wthit:$platform-$wthit_version") // run against the full jar
            modLocalRuntime("lol.bai:badpackets:$platform-$badpackets_version") // for WTHIT
        } else if (waila_fork != "jade") { // jade runtime is handled per-subproject
            println("Unknown WAILA fork, must be 'jade' or 'wthit': " + waila_fork)
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    targetCompatibility = JavaVersion.VERSION_17
    sourceCompatibility = JavaVersion.VERSION_17

    group = "io.github.consistencyplus"
    version = "$rootProject.mod_version+$rootProject.minecraft_version"

    tasks.withType(JavaCompile).forEach {
        it.options.release.set(17)
        it.options.encoding = "UTF-8"
    }

    repositories {
        maven { url = "https://maven.quiltmc.org/repository/release/" }
        maven { url = "https://maven.quiltmc.org/repository/snapshot/" }
        maven { url = "https://maven.bai.lol" } // WTHIT
        maven { url = "https://api.modrinth.com/maven" }
        maven { url = "https://maven.blamejared.com" } // Patchouli
    }

    jar {
        from "LICENSE"
        exclude "**/upcoming/**"
        exclude "**/unused/**"
    }

    java.withSourcesJar()
}
