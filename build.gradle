plugins {
    id "fabric-loom" version "0.9.+"
    id "io.github.juuxel.loom-quiltflower" version "1.+"
}

String mod_version = "0.4.1"

String minecraft_version = "1.17.1"
String mc_major_version = "1.17"
String yarn_mappings  = "46"     // https://fabricmc.net/versions.html
String loader_version = "0.11.6" // https://fabricmc.net/versions.html
String fabric_version = "0.39.2" // https://fabricmc.net/versions.html
String srt_version    = "1.0.4"  // https://modrinth.com/mod/stonecutter_recipe_tags/versions

archivesBaseName = "consistency_plus"
version = mod_version + "+" + mc_major_version

repositories {
    maven {url = "https://api.modrinth.com/maven"}
    maven {url = "https://maven.waffle.coffee"}
}

Set<String> apiModules = [
        "fabric-api-base",
        "fabric-blockrenderlayer-v1",
        "fabric-item-api-v1",
        "fabric-item-groups-v0",
        "fabric-lifecycle-events-v1",
        "fabric-object-builder-api-v1",
        "fabric-rendering-v1"
]

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings "net.fabricmc:yarn:${minecraft_version}+build.${yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${loader_version}"
    apiModules.forEach { include(modImplementation(fabricApi.module(it, "${fabric_version}+${mc_major_version}"))) }

    modImplementation include("maven.modrinth:stonecutter_recipe_tags:${srt_version}")

    modImplementation "maven.modrinth:lazydfu:0.1.2" // Reduces start time
}

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) { it.options.encoding = "UTF-8" }

jar { from("LICENSE") }
