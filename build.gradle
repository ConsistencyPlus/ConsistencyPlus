//file:noinspection GroovyAssignabilityCheck
plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.11.0-SNAPSHOT" apply false
    id 'io.github.juuxel.loom-quiltflower' version '1.+' apply false
    id "maven-publish"
}

version = rootProject.mod_version + "+" + rootProject.minecraft_version

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "io.github.juuxel.loom-quiltflower"

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings "net.fabricmc:yarn:${rootProject.minecraft_version}+build.${rootProject.mappings_version}:v2"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version + "+" + rootProject.minecraft_version

    tasks.withType(JavaCompile) {
        options.release = 17
        it.options.encoding = "UTF-8"
    }

    repositories {
        maven {
            url = "https://api.modrinth.com/maven"
            content { includeGroup "maven.modrinth" }
        }
        maven {
            url = "https://maven.bai.lol"
            content { includeGroup "mcp.mobius.waila" }
        }
        maven {
            url = "https://maven.withertech.com/repository/withertech"
        }
    }

    jar {
        from("LICENSE")
        exclude("**/upcoming/**")
        exclude("**/unused/**")
    }

    java { withSourcesJar() }
}
