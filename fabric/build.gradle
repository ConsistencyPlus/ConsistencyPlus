apply from: "../gradle/loader.gradle"

architectury.fabric()

configurations {
    developmentFabric.extendsFrom common
}

repositories {
    maven { url = "https://maven.terraformersmc.com" } // Mod Menu
}

dependencies {
    modImplementation("net.fabricmc:fabric-loader:$fabric_version")
    modApi("net.fabricmc.fabric-api:fabric-api:$fabric_api_version")
    include("maven.modrinth:stonecutter_recipe_tags:$scrt_version-fabric") // we can include SCRT on fabric

    modLocalRuntime("vazkii.patchouli:Patchouli:$patchouli_version-FABRIC")
    if (waila_fork == "jade")
        modLocalRuntime("maven.modrinth:jade:$jade_fabric_version_id")

    // dev QOL
    modRuntimeOnly("maven.modrinth:lazydfu:0.1.3")
    modImplementation("maven.modrinth:isnrmn:v1_03")
    modRuntimeOnly("com.terraformersmc:modmenu:${rootProject.mod_menu_version}") { transitive = false }

    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

sourceSets.main {
    resources {
        srcDirs += [
                'src/generated/resources'
        ]
    }
}

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

publishing.publications {
    mavenFabric(MavenPublication) from components.java
}
