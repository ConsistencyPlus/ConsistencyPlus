apply plugin: "com.github.johnrengelman.shadow"

architectury.platformSetupLoomIde()

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
}

dependencies {
    common(project(path: ":common", configuration: "namedElements")) { transitive false }
}

processResources {
    // these files must be in C+'s jar directly for reading.
    // if they are in the generated common jar, stuff won't work.
    from(rootProject.file("common/src/main/resources")) {
        include("resourcepacks/**")
    }
    from(rootProject.file("common/src/main/resources")) {
        include("materials/**")
    }
    from(rootProject.file("common/src/main/resources")) {
        include("materials.json")
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

jar.archiveClassifier = "dev"

sourcesJar {
    duplicatesStrategy(DuplicatesStrategy.WARN)
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing.repositories.maven {
    url = System.getenv().MAVEN_URL
    credentials {
        username = System.getenv().MAVEN_USER
        password = System.getenv().MAVEN_PASS
    }
    authentication { basic(BasicAuthentication) }
}