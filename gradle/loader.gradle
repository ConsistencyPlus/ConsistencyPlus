apply plugin: "com.github.johnrengelman.shadow"

architectury.platformSetupLoomIde()

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
}

dependencies {
    common(project(path: ":common", configuration: "namedElements")) { transitive false }
}

processResources {
    from(rootProject.file("common/src/main/resources")) {
        include("resourcepacks")
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
}

jar.classifier = "dev"

sourcesJar {
    duplicatesStrategy(DuplicatesStrategy.WARN)
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing.repositories.maven {
    url = System.getenv().MAVEN_URL
    credentials {
        username = System.getenv().MAVEN_USER
        password = System.getenv().MAVEN_PASS
    }
    authentication { basic(BasicAuthentication) }
}